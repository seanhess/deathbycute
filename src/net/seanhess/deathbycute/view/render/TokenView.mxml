<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:model="net.seanhess.deathbycute.model.*"
	creationComplete="onComplete()"
	>
	
	<mx:Script>
		<![CDATA[
			import net.seanhess.deathbycute.model.*;
			import assets.Other;
			
			import mx.effects.easing.Linear;
			public static const MS_PER_PIXEL:int = 1;
			
			protected var complete:Boolean = false;
			
			protected function onComplete():void
			{
				this.x = token.location.x;
				this.y = token.location.y;
				complete = true;
				restartAnimation();
			}
			
			public function set destination(value:Point):void
			{
				restartAnimation();
			}
			
			protected function restartAnimation():void
			{
				if (complete == false || token.desintation == null)
					return;
				
				var value:Point = token.desintation;
				
				animate.stop();
				
				var totalDistance:int = Math.abs(value.x - token.location.x) + Math.abs(value.y - token.location.y);
				
				goX.fromValue = token.location.x;
				goX.toValue = value.x - (width/2);
				goX.duration = MS_PER_PIXEL * totalDistance; 
				
				
				goY.fromValue = token.location.y;
				goY.toValue = value.y - (height/2);
				goY.duration = MS_PER_PIXEL * totalDistance;
				
				animate.play();
			}
			
			protected function onTweenUpdate():void
			{
				token.location.x = this.x;
				token.location.y = this.y;
			}
			
			protected function onEnd():void
			{
				// set firing to false // 
				if (token is Weapon)
				{
					(token as Weapon).owner.firing = null;
				}
			}
				
		]]>
	</mx:Script>
	
	<mx:Binding source="token.desintation" destination="destination"/>
	
	<mx:Image source="{token.image}" alpha="{(token.state == Character.DEAD) ? 0.6 : 1}"/>
	<mx:Image source="{Other.DEATH_EYES}" visible="{token.state == Character.DEAD}"/>
	
	<mx:Parallel id="animate" target="{this}" effectEnd="onEnd()">
		<mx:AnimateProperty id="goX" property="x" easingFunction="{Linear.easeNone}" tweenUpdate="onTweenUpdate()"/>
		<mx:AnimateProperty id="goY" property="y" easingFunction="{Linear.easeNone}"/>
	</mx:Parallel>
	
	<model:Token id="token"/>
	
</mx:Canvas>
